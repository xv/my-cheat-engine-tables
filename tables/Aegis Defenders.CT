<?xml version="1.0" encoding="utf-8"?>
<CheatTable CheatEngineTableVersion="31">
  <CheatEntries>
    <CheatEntry>
      <ID>0</ID>
      <Description>"Enable Cheat Table"</Description>
      <Options moHideChildren="1" moDeactivateChildrenAsWell="1"/>
      <LastState/>
      <VariableType>Auto Assembler Script</VariableType>
      <AssemblerScript>[ENABLE]
{$lua}
if process == "AegisDefenders.exe" and not monopipe then
    if LaunchMonoDataCollector() == 0 then
        showMessage("Failed to activate mono features")
    end
end
{$asm}

[DISABLE]
{$lua}
if monopipe then
    monopipe.OnTimeout()
end
{$asm}
</AssemblerScript>
      <CheatEntries>
        <CheatEntry>
          <ID>1</ID>
          <Description>"God Mode"</Description>
          <LastState/>
          <VariableType>Auto Assembler Script</VariableType>
          <AssemblerScript>define(address, "DestructibleHero:GotTriggerin"+6)
define(originalBytes, 81 EC 9C 00 00 00)

[ENABLE]
assert(address, originalBytes)
alloc(memHeroInvincible, 15)

label(return)

memHeroInvincible:
  sub esp, 9C
  jmp "DestructibleHero:GotTriggerin"+4e9

address:
  jmp memHeroInvincible
  nop
return:

[DISABLE]
address:
  // sub esp, 0000009C
  db originalBytes

dealloc(memHeroInvincible)
</AssemblerScript>
        </CheatEntry>
        <CheatEntry>
          <ID>2</ID>
          <Description>"One Hit Kill"</Description>
          <LastState/>
          <VariableType>Auto Assembler Script</VariableType>
          <AssemblerScript>define(address, "DestructibleEnemy:TakeDamage"+11a)
define(originalBytes, 8B 4D 0C F7 D9)

[ENABLE]
assert(address, originalBytes)
alloc(memSetDamageAmount, 20)

label(return)

memSetDamageAmount:
  mov ecx, 3E7 // Deal 999 damage with every hit
  neg ecx      // Remove this line and you'd add 999 HP ;)
  jmp return

address:
  jmp memSetDamageAmount
return:

[DISABLE]
address:
  // mov ecx,[ebp+0C]
  // neg ecx
  db originalBytes

dealloc(memSetDamageAmount)
</AssemblerScript>
        </CheatEntry>
        <CheatEntry>
          <ID>3</ID>
          <Description>"Super Jump"</Description>
          <LastState/>
          <VariableType>Auto Assembler Script</VariableType>
          <AssemblerScript>define(address, "Hero:Jump"+b9)
define(originalBytes, 83 C4 0C D9 45 0C)

[ENABLE]
assert(address, originalBytes)
alloc(memNewJumpValue, 20)

label(return)

memNewJumpValue:
  add esp, 0C
  mov [ebp+0C], (float)30.0 // Game default is ~18.0
  fld dword ptr [ebp+0C]
  jmp return

address:
  jmp memNewJumpValue
  nop
return:

[DISABLE]
address:
  // add esp, 0C
  // fld dword ptr [ebp+0C]
  db originalBytes

dealloc(memNewJumpValue)
</AssemblerScript>
        </CheatEntry>
        <CheatEntry>
          <ID>4</ID>
          <Description>"Infinite Building Resources"</Description>
          <LastState/>
          <VariableType>Auto Assembler Script</VariableType>
          <AssemblerScript>define(address, "HeroBuildArea:UpdateCanAffordItem"+8e)
define(originalBytes, C6 80 AC 00 00 00 00)

[ENABLE]
assert(address, originalBytes)
alloc(memInfiniteBuildRes, 200)

label(checkHeroBart)
label(checkHeroClu)
label(checkHeroKaiim)
label(checkHeroZula)
label(setResAmount)

label(return)

memInfiniteBuildRes:
  mov byte ptr [eax+000000AC], 1 // canAffordItem = true
  ///////////////////////////////////////
  // Get the current hero index to decide
  // what resource to refill
  //     Bart  = 1
  //     Clu   = 2
  //     Kaiim = 5
  //     Zula  = 6
  ///////////////////////////////////////
  mov eax, [eax+40]
  mov eax, [eax+000001C8]
  ///////////////////////////////////////
  // Get resource wallet size first from
  // instance.currentGameData.walletSize
  ///////////////////////////////////////
  cmp [esi], esi
  mov ebx, [esi+34]
  mov ecx, [ebx+54]

checkHeroBart:
  cmp eax, 1
  jne checkHeroClu
  mov ebx, 0 // RESOURCE_DEFENSE
  jmp setResAmount
checkHeroClu:
  cmp eax, 2
  jne checkHeroKaiim
  mov ebx, 1 // RESOURCE_OFFENSE
  jmp setResAmount
checkHeroKaiim:
  cmp eax, 5
  jne checkHeroZula
  mov ebx, 6 // RESOURCE_BLUE
  jmp setResAmount
checkHeroZula:
  cmp eax, 6
  jne setResAmount
  mov ebx, 7 // RESOURCE_PURPLE
setResAmount:
  sub esp, 04
  push ecx // Refill amount
  push ebx // Resource type
  push esi
  cmp [esi], esi
  call "DataManager:SetResourceAmount"
  add esp, 10
  jmp return

address:
  jmp memInfiniteBuildRes
  nop 2
return:

[DISABLE]
address:
  // mov byte ptr [eax+000000AC], 00
  db originalBytes

dealloc(memInfiniteBuildRes)
</AssemblerScript>
        </CheatEntry>
        <CheatEntry>
          <ID>5</ID>
          <Description>"Freeze Fragile Platforms"</Description>
          <LastState/>
          <VariableType>Auto Assembler Script</VariableType>
          <AssemblerScript>define(address, "DropPlatform:OnCollisionEnter2D"+5)
define(originalBytes, 83 EC 30 8B 75 08)

[ENABLE]
assert(address, originalBytes)
alloc(memFreezeFragilePlatform, 15)

label(return)

memFreezeFragilePlatform:
  sub esp, 30
  jmp "DropPlatform:OnCollisionEnter2D"+e9

address:
  jmp memFreezeFragilePlatform
  nop
return:

[DISABLE]
address:
  // sub esp, 30
  // mov esi, [ebp+08]
  db originalBytes

dealloc(memFreezeFragilePlatform)
</AssemblerScript>
        </CheatEntry>
        <CheatEntry>
          <ID>6</ID>
          <Description>"Freeze Manned Turret Stamina"</Description>
          <LastState/>
          <VariableType>Auto Assembler Script</VariableType>
          <AssemblerScript>define(address, "TurretControl:SpendStamina"+4)
define(originalBytes, 83 EC 04 8B 7D 08)

[ENABLE]
assert(address, originalBytes)
alloc(memFreezeTurretStamina, 15)

label(return)

memFreezeTurretStamina:
  sub esp, 04
  jmp "TurretControl:SpendStamina"+be

address:
  jmp memFreezeTurretStamina
  nop
return:

[DISABLE]
address:
  // sub esp, 04
  // mov edi, [ebp+08]
  db originalBytes

dealloc(memFreezeTurretStamina)
</AssemblerScript>
        </CheatEntry>
        <CheatEntry>
          <ID>7</ID>
          <Description>"Shop Purchases Cost Nothing"</Description>
          <LastState/>
          <VariableType>Auto Assembler Script</VariableType>
          <AssemblerScript>define(addressCheckCost, "ShopItemCard:CheckItemCost"+9)
define(originalBytesCheckCost, C7 45 EC 00 00 00 00)

define(addressTryPurchase, "ShopItemCard:TryPurchaseItem"+1c4)
define(originalBytesTryPurchase, 8B 4D F4 F7 D9)

define(addressUpdateCost, "ItemDescriptionPanel:UpdateCost"+74)
define(originalBytesUpdateCost, 8D 45 C0 05 04 00 00 00)

[ENABLE]
assert(addressCheckCost, originalBytesCheckCost)
alloc(memCheckCost, 10)

assert(addressTryPurchase, originalBytesTryPurchase)
alloc(memSpendNothing, 10)

assert(addressUpdateCost, originalBytesUpdateCost)
alloc(memSetItemCostText, 10)

///////////////////////////////////////////////
// Always return TRUE so you can purchase items
// regardless of how much RP or Chip you have
///////////////////////////////////////////////
memCheckCost:
  mov eax, 1
  jmp "ShopItemCard:CheckItemCost"+231

addressCheckCost:
  jmp memCheckCost
  nop 2


///////////////////////////////////////////////
// Deduct 0 RP or Chip when confirming an item
// purchase from any of the shops
///////////////////////////////////////////////
memSpendNothing:
  mov ecx, 0
  jmp "ShopItemCard:TryPurchaseItem"+1c9

addressTryPurchase:
  jmp memSpendNothing


///////////////////////////////////////////////
// Set the text of shop item prices to 0
///////////////////////////////////////////////
memSetItemCostText:
  add eax, 00000004
  jmp "ItemDescriptionPanel:UpdateCost"+7c

addressUpdateCost:
  jmp memSetItemCostText
  db 90 90 90

[DISABLE]
addressCheckCost:
  // mov [ebp-14], 00000000
  db originalBytesCheckCost

addressTryPurchase:
  // mov ecx, [ebp-0C]
  // neg ecx
  db originalBytesTryPurchase

addressUpdateCost:
  // lea eax, [ebp-40]
  // add eax, 00000004
  db originalBytesUpdateCost

dealloc(memCheckCost)
dealloc(memSpendNothing)
dealloc(memSetItemCostText)
</AssemblerScript>
        </CheatEntry>
      </CheatEntries>
    </CheatEntry>
  </CheatEntries>
  <UserdefinedSymbols/>
</CheatTable>
